! computation of thermal optical depths (dB/dT weights)
SUBROUTINE ACRANEB_TRANST_DELTA_T(KIDIA,KFDIA,KLON,KLEV,KL,PRES,PT,PDU,PUW,PUS,PUS_IRHOV,PUC,&
 & P_U,P_PU,P_TU,PDELTA,YDPHY,YDPHY3,PTRLI,PIRHOV,PT_FW,PT_FS,PT_FC)

USE PARKIND1 ,ONLY : JPIM     ,JPRB     ,JPRD
USE YOMPHY   ,ONLY : TPHY
USE YOMPHY3  ,ONLY : TPHY3
USE YOMCST   ,ONLY : RPI 

IMPLICIT NONE

INTEGER(KIND=JPIM), INTENT(IN)    :: KLON
INTEGER(KIND=JPIM), INTENT(IN)    :: KLEV
INTEGER(KIND=JPIM), INTENT(IN)    :: KIDIA
INTEGER(KIND=JPIM), INTENT(IN)    :: KFDIA
INTEGER(KIND=JPIM), INTENT(IN)    :: KL                 ! current level
REAL   (KIND=JPRB), INTENT(IN)    :: PRES     (KLON)    ! pressure
REAL   (KIND=JPRB), INTENT(IN)    :: PT       (KLON)    ! temperature
REAL   (KIND=JPRB), INTENT(IN)    :: PDU      (KLON,4)  ! 2.du
REAL   (KIND=JPRB), INTENT(INOUT) :: PUW      (KLON,3)  ! u_w
REAL   (KIND=JPRB), INTENT(INOUT) :: PUS      (KLON,3)  ! u_s
REAL   (KIND=JPRB), INTENT(INOUT) :: PUS_IRHOV(KLON,3)  ! u_s/rho
REAL   (KIND=JPRB), INTENT(INOUT) :: PUC      (KLON)    ! u_c
REAL   (KIND=JPRB), INTENT(INOUT) :: P_U      (KLON,3)  ! u
REAL   (KIND=JPRB), INTENT(INOUT) :: P_PU     (KLON,3)  ! p_avg.u
REAL   (KIND=JPRB), INTENT(INOUT) :: P_TU     (KLON,3)  ! T_avg.u
REAL   (KIND=JPRB), INTENT(OUT)   :: PDELTA   (KLON)    ! optical depth
TYPE(TPHY),         INTENT(IN)    :: YDPHY
TYPE(TPHY3),        INTENT(IN)    :: YDPHY3
REAL   (KIND=JPRB), INTENT(IN)    :: PTRLI
REAL   (KIND=JPRB), INTENT(IN)    :: PIRHOV   (KLON,0:KLEV)
REAL   (KIND=JPRB), INTENT(IN)    :: PT_FW(KLON,0:KLEV,3)
REAL   (KIND=JPRB), INTENT(IN)    :: PT_FS(KLON,0:KLEV,3)
REAL   (KIND=JPRB), INTENT(IN)    :: PT_FC(KLON,0:KLEV)

INTEGER(KIND=JPIM) :: JO_

REAL(KIND=JPRB) :: Z4BU_,ZA2B_,ZAFVOI_,ZVOIEMP_,ZVOIGT_
REAL(KIND=JPRB) :: ZAUX_,ZLOG_,ZP_AVG_,ZT_AVG_

REAL(KIND=JPRB) :: ZDELTA_(KLON,3)
REAL(KIND=JPRB) :: ZA_    (KLON,3)
REAL(KIND=JPRB) :: ZAR_   (KLON,3)
REAL(KIND=JPRB) :: ZCOEF_ (KLON,3)
REAL(KIND=JPRB) :: ZTAU_  (KLON,3)

REAL(KIND=JPRB) :: ZT_RHOZ0V (3)
REAL(KIND=JPRB) :: ZMD
INTEGER(KIND=JPIM) :: JG,JLON
REAL(KIND=JPRB) :: ZVOISIM, ZEPSD
REAL(KIND=JPRB) :: ZEPSV,ZGAMV,ZIBV0,ZIZV0

ASSOCIATE(FGTT_OC=>YDPHY3%FGTT_OC, FGTT_OB=>YDPHY3%FGTT_OB, &
 & FGTC_D=>YDPHY3%FGTC_D, FGTT_OA=>YDPHY3%FGTT_OA, FGTC_A=>YDPHY3%FGTC_A, &
 & FGTC_C=>YDPHY3%FGTC_C, FGTC_B=>YDPHY3%FGTC_B, FGTC_P00=>YDPHY3%FGTC_P00, &
 & FGTC_Q=>YDPHY3%FGTC_Q, FGTC_P=>YDPHY3%FGTC_P, FGTT_D=>YDPHY3%FGTT_D, &
 & FGTT_B=>YDPHY3%FGTT_B, FGTT_C=>YDPHY3%FGTT_C, FGTT_A=>YDPHY3%FGTT_A, &
 & RTL=>YDPHY3%RTL, FGTC_DELTA0=>YDPHY3%FGTC_DELTA0, FGTT_P=>YDPHY3%FGTT_P, &
 & FGTT_Q=>YDPHY3%FGTT_Q, FGTC_OC=>YDPHY3%FGTC_OC, FGTC_OB=>YDPHY3%FGTC_OB, &
 & FGTC_OA=>YDPHY3%FGTC_OA, FGTT_DELTA0=>YDPHY3%FGTT_DELTA0, &
 & FGTC_OD=>YDPHY3%FGTC_OD, FGTT_OD=>YDPHY3%FGTT_OD, &
 & FGTT_ALPHA=>YDPHY3%FGTT_ALPHA, FGTT_P00=>YDPHY3%FGTT_P00, &
 & FGTC_ALPHA=>YDPHY3%FGTC_ALPHA, &
 & LVOIGT=>YDPHY%LVOIGT, LVFULL=>YDPHY%LVFULL, LRPROX=>YDPHY%LRPROX)

! ratio of diffusivity factors sqrt(e) and 2 (the latter is used in
! weak line limit)
ZMD=0.5_JPRB*EXP(0.5_JPRB)

! Voigt coefficients
ZT_RHOZ0V(1)=0.031_JPRB
ZT_RHOZ0V(2)=0.013_JPRB
ZT_RHOZ0V(3)=0.012_JPRB

ZVOISIM     =1.21_JPRB

IF (JPRB == JPRD) THEN
  ZEPSD =1.E-20_JPRB
ELSE
  ZEPSD =1.E-10_JPRB
ENDIF

ZGAMV       =0.36_JPRB
ZEPSV       =0.014_JPRB
ZIBV0       =1._JPRB/0.74_JPRB
ZIZV0       =1._JPRB/0.0027_JPRB

! compute optical depths including narrowband saturation
DO JG=1,3
  DO JLON=KIDIA,KFDIA

    ! update quantities u_w, u_s and u_s_rho
    PUW(JLON,JG)=PUW(JLON,JG)+PT_FW(JLON,KL,JG)*PDU(JLON,JG)
    PUS(JLON,JG)=PUS(JLON,JG)+PT_FS(JLON,KL,JG)*PDU(JLON,JG)*ZMD
    PUS_IRHOV(JLON,JG)=PUS_IRHOV(JLON,JG)+PIRHOV(JLON,KL)*&
     &                        PT_FS(JLON,KL,JG)*PDU(JLON,JG)*ZMD

    ! Malkmus-Voigt formula with Curtis-Godson approximation and
    ! rescaled absorber amount
    ZA2B_  =PUS(JLON,JG)/(2._JPRB*PUW(JLON,JG))
    Z4BU_  =4._JPRB*PUW(JLON,JG)*PUW(JLON,JG)/PUS(JLON,JG)
    ZAFVOI_=1._JPRB
    IF (LVOIGT) THEN
      ZVOIGT_=ZT_RHOZ0V(JG)*PUS_IRHOV(JLON,JG)/PUS(JLON,JG)
      IF (LVFULL) THEN
        ZVOIEMP_=EXP(ZEPSV*LOG(ZIBV0*(Z4BU_/ZVOIGT_))*LOG(ZIZV0*ZVOIGT_))
      ELSE
        ZVOIEMP_=ZVOISIM
      ENDIF
      ZAFVOI_=1._JPRB+ZVOIGT_/(RPI*(Z4BU_/ZVOIGT_)/(1.5_JPRB*SQRT(Z4BU_))+&
       & 4._JPRB/(Z4BU_/ZVOIGT_)+5._JPRB+ZGAMV*SQRT(Z4BU_/ZVOIGT_)*ZVOIEMP_)
    ENDIF
    ZDELTA_(JLON,JG)=ZA2B_*(SQRT(1._JPRB+ZAFVOI_*Z4BU_)-1._JPRB)

  ENDDO
ENDDO

! add H2O e-type continuum
DO JLON=KIDIA,KFDIA
  PUC(JLON)=PUC(JLON)+PT_FC(JLON,KL)*PDU(JLON,4)*ZMD
  ZDELTA_(JLON,1)=ZDELTA_(JLON,1)+PUC(JLON)/&
   & (1._JPRB+FGTT_C(3)*PUC(JLON))+FGTT_C(4)*PUC(JLON)**FGTT_C(5)
ENDDO

! compute broadband saturation
DO JG=1,3
  DO JLON=KIDIA,KFDIA

    ! rescaling in order to account for broadband saturation
    ZDELTA_(JLON,JG)=(FGTT_DELTA0(JG)/FGTT_ALPHA(JG))*((1._JPRB+&
     & ZDELTA_(JLON,JG)/FGTT_DELTA0(JG))**FGTT_ALPHA(JG)-1._JPRB)

    ! update u and T_avg.u, compute T_avg
    P_U (JLON,JG)=P_U (JLON,JG)+           PDU(JLON,JG)
    P_PU(JLON,JG)=P_PU(JLON,JG)+PRES(JLON)*PDU(JLON,JG)
    P_TU(JLON,JG)=P_TU(JLON,JG)+PT  (JLON)*PDU(JLON,JG)
    ZP_AVG_=P_PU(JLON,JG)/P_U(JLON,JG)
    ZT_AVG_=P_TU(JLON,JG)/P_U(JLON,JG)

    ! corrective secondary fit
    ZAUX_=ZDELTA_(JLON,JG)/(ZDELTA_(JLON,JG)+FGTT_D(JG))
    ZLOG_=LOG(MAX(ZDELTA_(JLON,JG),PTRLI))
    ZDELTA_(JLON,JG)=ZDELTA_(JLON,JG)*MAX(0._JPRB,&
     & FGTT_P00(JG,0)+ZT_AVG_*(FGTT_P00(JG,1)+ZT_AVG_*FGTT_P00(JG,2))+&
     & ZAUX_*(FGTT_P(JG,0,0)+ZT_AVG_*(FGTT_P(JG,0,1)+ZT_AVG_*FGTT_P(JG,0,2))+&
     & ZLOG_*(FGTT_P(JG,1,0)+ZT_AVG_*(FGTT_P(JG,1,1)+ZT_AVG_*FGTT_P(JG,1,2))+&
     & ZLOG_*(FGTT_P(JG,2,0)+ZT_AVG_*(FGTT_P(JG,2,1)+ZT_AVG_*FGTT_P(JG,2,2))+&
     & ZLOG_*(FGTT_P(JG,3,0)+ZT_AVG_*(FGTT_P(JG,3,1)+ZT_AVG_*FGTT_P(JG,3,2))+&
     & ZLOG_*(FGTT_P(JG,4,0)+ZT_AVG_*(FGTT_P(JG,4,1)+ZT_AVG_*FGTT_P(JG,4,2))+&
     & ZLOG_*(FGTT_P(JG,5,0)+ZT_AVG_*(FGTT_P(JG,5,1)+ZT_AVG_*FGTT_P(JG,5,2))&
     & )))))))
    ZDELTA_(JLON,JG)=ZDELTA_(JLON,JG)*MAX(0._JPRB,1._JPRB+&
     & (FGTT_Q(JG,0)+ZP_AVG_*(FGTT_Q(JG,1)+ZP_AVG_*FGTT_Q(JG,2)))/&
     & (1._JPRB+ZDELTA_(JLON,JG)))
  ENDDO
ENDDO

! compute gaseous overlaps

! individual absorptivities
DO JLON=KIDIA,KFDIA
  ZA_(JLON,1)=1._JPRB-EXP(-ZDELTA_(JLON,1))
  ZA_(JLON,2)=1._JPRB-EXP(-ZDELTA_(JLON,2))
  ZA_(JLON,3)=1._JPRB-EXP(-ZDELTA_(JLON,3))
ENDDO

! absorptivities for pairs of gases assuming random overlaps and
! modulation factors for the fits
DO JLON=KIDIA,KFDIA
  ZAR_(JLON,1)=ZA_(JLON,1)+ZA_(JLON,2)-ZA_(JLON,1)*ZA_(JLON,2)
  ZAR_(JLON,2)=ZA_(JLON,1)+ZA_(JLON,3)-ZA_(JLON,1)*ZA_(JLON,3)
  ZAR_(JLON,3)=ZA_(JLON,2)+ZA_(JLON,3)-ZA_(JLON,2)*ZA_(JLON,3)
  ZCOEF_(JLON,1)=2._JPRB*ZA_(JLON,1)*ZA_(JLON,2)/&
   & (ZEPSD+ZA_(JLON,1)*ZA_(JLON,1)+ZA_(JLON,2)*ZA_(JLON,2))
  ZCOEF_(JLON,2)=2._JPRB*ZA_(JLON,1)*ZA_(JLON,3)/&
   & (ZEPSD+ZA_(JLON,1)*ZA_(JLON,1)+ZA_(JLON,3)*ZA_(JLON,3))
  ZCOEF_(JLON,3)=2._JPRB*ZA_(JLON,2)*ZA_(JLON,3)/&
   & (ZEPSD+ZA_(JLON,2)*ZA_(JLON,2)+ZA_(JLON,3)*ZA_(JLON,3))
ENDDO

! transmissions for pairs of gases containing fitted contribution
! of non-random overlaps
DO JO_=1,3
  DO JLON=KIDIA,KFDIA
    ZTAU_(JLON,JO_)=1._JPRB-ZAR_(JLON,JO_)
  ENDDO
  IF ( FGTT_OA(JO_) /= 0._JPRB ) THEN
    DO JLON=KIDIA,KFDIA
      ZTAU_(JLON,JO_)=ZTAU_(JLON,JO_)-ZCOEF_(JLON,JO_)*FGTT_OA(JO_)*&
       & (1._JPRB-ZAR_(JLON,JO_))**FGTT_OB(JO_)*ZAR_(JLON,JO_)**FGTT_OC(JO_)*&
       & (1._JPRB-FGTT_OD(JO_)*ZAR_(JLON,JO_))
    ENDDO
  ENDIF
ENDDO

! final optical depth (cannot go below maximum of individual optical depths)
DO JLON=KIDIA,KFDIA
  PDELTA(JLON)=-LOG(MAX(PTRLI,ZTAU_(JLON,1)*ZTAU_(JLON,2)*ZTAU_(JLON,3)))-&
   & (ZDELTA_(JLON,1)+ZDELTA_(JLON,2)+ZDELTA_(JLON,3))
ENDDO
DO JLON=KIDIA,KFDIA
  PDELTA(JLON)=MAX(PDELTA(JLON),&
   & ZDELTA_(JLON,1),ZDELTA_(JLON,2),ZDELTA_(JLON,3))
ENDDO

END ASSOCIATE

END SUBROUTINE ACRANEB_TRANST_DELTA_T
